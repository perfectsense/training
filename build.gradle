plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'com.github.node-gradle.node' version '1.3.0'
}

ext {
    brightspotVersion = '4.2.24'
    brightspotGradlePluginVersion = '4.2.9'
    componentLibVersion = '4.2.159'
    goVersion = '0.10.0'
}

allprojects {
    group = 'brightspot-training'

    if (project.hasProperty('release')) {
      version = project.release
    } else {
      version = '1.0.0-SNAPSHOT'
    }

    defaultTasks 'build'

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('commons-logging:commons-logging') with module('org.slf4j:jcl-over-slf4j:0')
                substitute module('com.github.bbottema:jetbrains-runtime-annotations') with module('org.jetbrains:annotations:0')
                substitute module('com.google.android.tools:dx') with module('junit:junit:0')
                substitute module('com.sun.mail:javax.mail') with module('com.sun.mail:jakarta.mail:1.6.5')
                substitute module('javax.activation:activation') with module('com.sun.activation:jakarta.activation:1.2.1')
                substitute module('javax.activation:javax.activation-api') with module('com.sun.activation:jakarta.activation:1.2.1')
                substitute module('jakarta.activation:jakarta.activation-api') with module('com.sun.activation:jakarta.activation:1.2.1')
                substitute module('javax.annotation:javax.annotation-api') with module('jakarta.annotation:jakarta.annotation-api:1.3.5')
            }
        }
    }
}

apply from: "https://artifactory.psdops.com/psddev-releases/com/psddev/brightspot-gradle-plugins/express/${brightspotGradlePluginVersion}/express.gradle"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.node-gradle.node'

    tasks.withType(JavaCompile) {
        if (JavaVersion.current().isJava12Compatible()) {
            options.release = 11

        } else {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
        options.encoding = 'UTF-8'
    }

    dependencies {

        implementation enforcedPlatform("com.brightspot.go:bom:$goVersion")
        implementation enforcedPlatform("com.brightspot.go:go-dependencies:$goVersion")
        implementation enforcedPlatform("com.psddev:brightspot-bom:$brightspotVersion")
        implementation enforcedPlatform("com.psddev:brightspot-dependencies:$brightspotVersion")
        implementation enforcedPlatform("com.psddev.component-lib:bom:$componentLibVersion")
    }

    node {
        yarnVersion = '1.12.3'
        version = '12.18.0'
        download = true
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://artifactory.psdops.com/public' }
    }

    test {
        useJUnitPlatform()
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}
