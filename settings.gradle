pluginManagement {
    repositories {
        gradlePluginPortal() {
            content {
                excludeGroupByRegex 'com\\.brightspot($|\\..+)'
                excludeGroupByRegex 'com\\.psddev($|\\..+)'
            }
        }
        maven {
            url 'https://artifactory.psdops.com/public'
        }
    }
}

plugins {
    id 'com.gradle.develocity' version '3.17.4'
    id 'com.brightspot.gradle' version '3.2.1'
}

dependencyResolutionManagement {
    versionCatalogs {
        brightspotLibs {
            library('brightspot', 'com.psddev:brightspot-bom:4.8.0')
            library('brightspotDependencies', 'com.psddev:brightspot-dependencies:4.8.0')

            library('brightspotGo', 'com.brightspot.go:bom:1.7.0')
            library('brightspotGoDependencies', 'com.brightspot.go:go-dependencies:1.7.0')

            library('brightspotSharedTests', 'com.brightspot.shared-tests:bom:1.3.4')

            library('componentLib', 'com.psddev.component-lib:bom:4.5.33')
            library('componentLibDependencies', 'com.psddev.component-lib:cl-dependencies:4.5.33')
        }
    }
}

rootProject.name = 'brightspot-training'

brightspot {
    projectGroup = 'com.brightspot.training'

    versions {
        java = 11
    }

    dependencySubstitutions = [
        'com.fasterxml.jackson.core:jackson-core': 'com.fasterxml.jackson.core:jackson-core:2.11.0',
        'com.fasterxml.jackson.core:jackson-databind': 'com.fasterxml.jackson.core:jackson-databind:2.11.0',
        'com.squareup.retrofit2:converter-jackson': 'com.squareup.retrofit2:converter-jackson:2.9.0',
        'javax.inject:javax.inject': 'org.glassfish.hk2.external:javax.inject:2.3.0-b05',
        'org.apache.solr:solr-solrj': 'org.apache.solr:solr-solrj:9.3.0',
    ]
}

include(':bundle-default')
include(':core')
include(':frontend')
include(':web')

project(':bundle-default').projectDir = file('frontend/bundles/default')

develocity {
    buildScan {
        publishing.onlyIf { false }
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"
    }
}
